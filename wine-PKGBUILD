# $Id$
# Maintainer: Sven-Hendrik Haase <sh@lutzhaase.com>
# Contributor: Jan "heftig" Steffens <jan.steffens@gmail.com>
# Contributor: Eduardo Romero <eduardo@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=wine
pkgver=1.9.13
pkgrel=1

_pkgbasever=${pkgver/rc/-rc}

source=(https://dl.winehq.org/wine/source/1.9/$pkgname-$_pkgbasever.tar.bz2)
sha1sums=('SKIP')

pkgdesc="A compatibility layer for running Windows programs"
url="http://www.winehq.com"
arch=(i686 x86_64)
options=(staticlibs)
license=(LGPL)

_depends=(
  fontconfig      lib32-fontconfig
  lcms2           lib32-lcms2
  libxml2         lib32-libxml2
  libxcursor      lib32-libxcursor
  libxrandr       lib32-libxrandr
  libxdamage      lib32-libxdamage
  libxi           lib32-libxi
  gettext         lib32-gettext
  freetype2       lib32-freetype2
  glu             lib32-glu
  libsm           lib32-libsm
  gcc-libs        lib32-gcc-libs
  libpcap         lib32-libpcap
  desktop-file-utils
)

makedepends=(autoconf ncurses bison perl fontforge flex
  'gcc>=4.5.0-2'  'gcc-multilib>=4.5.0-2'
  giflib                lib32-giflib
  libpng                lib32-libpng
  gnutls                lib32-gnutls
  libxinerama           lib32-libxinerama
  libxcomposite         lib32-libxcomposite
  libxmu                lib32-libxmu
  libxxf86vm            lib32-libxxf86vm
  libldap               lib32-libldap
  mpg123                lib32-mpg123
  openal                lib32-openal
  v4l-utils             lib32-v4l-utils
  libpulse              lib32-libpulse
  alsa-lib              lib32-alsa-lib
  libxcomposite         lib32-libxcomposite
  mesa                  lib32-mesa
  mesa-libgl            lib32-mesa-libgl
  libcl                 lib32-libcl
  libxslt               lib32-libxslt
  gst-plugins-base-libs lib32-gst-plugins-base-libs
  samba
  opencl-headers
)
  
optdepends=(
  giflib                lib32-giflib
  libpng                lib32-libpng
  libldap               lib32-libldap
  gnutls                lib32-gnutls
  mpg123                lib32-mpg123
  openal                lib32-openal
  v4l-utils             lib32-v4l-utils
  libpulse              lib32-libpulse
  alsa-plugins          lib32-alsa-plugins
  alsa-lib              lib32-alsa-lib
  libjpeg-turbo         lib32-libjpeg-turbo
  libxcomposite         lib32-libxcomposite
  libxinerama           lib32-libxinerama
  ncurses               lib32-ncurses
  libcl                 lib32-libcl
  libxslt               lib32-libxslt
  gst-plugins-base-libs lib32-gst-plugins-base-libs
  cups
  samba           dosbox
)

if [[ $CARCH == i686 ]]; then
  # Strip lib32 etc. on i686
  _depends=(${_depends[@]/*32-*/})
  makedepends=(${makedepends[@]/*32-*/} ${_depends[@]})
  makedepends=(${makedepends[@]/*-multilib*/})
  optdepends=(${optdepends[@]/*32-*/})
else
  makedepends=(${makedepends[@]} ${_depends[@]})
  provides=("bin32-wine=$pkgver" "wine-wow64=$pkgver")
  conflicts=('bin32-wine' 'wine-wow64')
  replaces=('bin32-wine')
fi

prepare() {
  # Allow ccache to work
  mv $pkgname-$_pkgbasever $pkgname

  sed 's|OpenCL/opencl.h|CL/opencl.h|g' -i $pkgname/configure*

  sed -i '/MS Shell Dlg 2/i HKLM,%FontSubStr%,MS Shell Dlg,,WenQuanYi Micro Hei' $pkgname/loader/wine.inf.in
  sed -i '/MS Shell Dlg 2/i HKLM,%FontSubStr%,FixedSys,,DejaVu Sans Mono' $pkgname/loader/wine.inf.in
  sed -i '/MS Shell Dlg 2/a HKLM,%FontSubStr%,System,,DejaVu Sans Mono' $pkgname/loader/wine.inf.in
  sed -i '/MS Shell Dlg 2/a HKLM,%FontSubStr%,Tahoma,,WenQuanYi Micro Hei' $pkgname/loader/wine.inf.in
  sed -i '/MS Shell Dlg 2/a HKLM,%FontSubStr%,PMingLiU,,WenQuanYi Micro Hei' $pkgname/loader/wine.inf.in
  sed -i '/Times New Roman TUR/a HKLM,%FontSubStr%,Tms Rmn,,WenQuanYi Micro Hei' $pkgname/loader/wine.inf.in
  sed -i 's/,FixedSys,/,"FixedSys",/g' $pkgname/loader/wine.inf.in
  sed -i 's/,MS Shell Dlg,/,"MS Shell Dlg",/g' $pkgname/loader/wine.inf.in
  sed -i 's/,Tahoma,/,"Tahoma",/g' $pkgname/loader/wine.inf.in
  sed -i 's/,PMingLiU,/,"PMingLiU",/g' $pkgname/loader/wine.inf.in
  sed -i 's/,Tms Rmn,/,"Tms Rmn",/g' $pkgname/loader/wine.inf.in
  sed -i 's/,System,,DejaVu Sans Mono/,"System",,"DejaVu Sans Mono"/g' $pkgname/loader/wine.inf.in
  sed -i 's/,WenQuanYi Micro Hei/,"WenQuanYi Micro Hei"/g' $pkgname/loader/wine.inf.in
  sed -i 's/,DejaVu Sans Mono/,"DejaVu Sans Mono"/g' $pkgname/loader/wine.inf.in
  sed -i 's/"Arial,186"/"WenQuanYi Micro Hei"/g' $pkgname/loader/wine.inf.in
  sed -i 's/"Arial,238"/"WenQuanYi Micro Hei"/g' $pkgname/loader/wine.inf.in
  sed -i 's/"Arial,204"/"WenQuanYi Micro Hei"/g' $pkgname/loader/wine.inf.in
  sed -i 's/"Arial,161"/"WenQuanYi Micro Hei"/g' $pkgname/loader/wine.inf.in
  sed -i 's/"Arial,162"/"WenQuanYi Micro Hei"/g' $pkgname/loader/wine.inf.in
  sed -i 's/"Courier New,186"/"WenQuanYi Micro Hei"/g' $pkgname/loader/wine.inf.in
  sed -i 's/"Courier New,238"/"WenQuanYi Micro Hei"/g' $pkgname/loader/wine.inf.in
  sed -i 's/"Courier New,204"/"WenQuanYi Micro Hei"/g' $pkgname/loader/wine.inf.in
  sed -i 's/"Courier New,161"/"WenQuanYi Micro Hei"/g' $pkgname/loader/wine.inf.in
  sed -i 's/"Courier New,162"/"WenQuanYi Micro Hei"/g' $pkgname/loader/wine.inf.in
  sed -i 's/"MS Sans Serif"/"WenQuanYi Micro Hei"/g' $pkgname/loader/wine.inf.in
  sed -i 's/"Arial"/"WenQuanYi Micro Hei"/g' $pkgname/loader/wine.inf.in
  sed -i 's/"Tahoma"/"WenQuanYi Micro Hei"/g' $pkgname/loader/wine.inf.in
  sed -i 's/"Times New Roman"/"WenQuanYi Micro Hei"/g' $pkgname/loader/wine.inf.in
  sed -i 's/"Times New Roman,186"/"WenQuanYi Micro Hei"/g' $pkgname/loader/wine.inf.in
  sed -i 's/"Times New Roman,238"/"WenQuanYi Micro Hei"/g' $pkgname/loader/wine.inf.in
  sed -i 's/"Times New Roman,204"/"WenQuanYi Micro Hei"/g' $pkgname/loader/wine.inf.in
  sed -i 's/"Times New Roman,161"/"WenQuanYi Micro Hei"/g' $pkgname/loader/wine.inf.in
  sed -i 's/"Times New Roman,162"/"WenQuanYi Micro Hei"/g' $pkgname/loader/wine.inf.in

  sed -i 's/InstallLanguage",,"0409/InstallLanguage",,"0404/g' $pkgname/loader/wine.inf.in
  sed -i 's/Default",,"0409/Default",,"0404/g' $pkgname/loader/wine.inf.in
  sed -i 's/,,"00000409/,,"00000404/g' $pkgname/loader/wine.inf.in

  sed -i '$a [Extra]' $pkgname/loader/wine.inf.in
  sed -i '$a HKCU,Control Panel\Desktop,"FontSmoothing",,"2"' $pkgname/loader/wine.inf.in

  # Get rid of old build dirs
  rm -rf $pkgname-{32,64}-build
  mkdir $pkgname-32-build
}

build() {
  cd "$srcdir"

  if [[ $CARCH == x86_64 ]]; then
    msg2 "Building Wine-64..."

    mkdir $pkgname-64-build
    cd "$srcdir/$pkgname-64-build"
    ../$pkgname/configure \
      --prefix=/usr \
      --libdir=/usr/lib \
      --with-x \
      --with-gstreamer \
      --enable-win64
    # Gstreamer was disabled for FS#33655

    make

    _wine32opts=(
      --libdir=/usr/lib32
      --with-wine64="$srcdir/$pkgname-64-build"
    )

    export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  fi

  msg2 "Building Wine-32..."
  cd "$srcdir/$pkgname-32-build"
  ../$pkgname/configure \
    --prefix=/usr \
    --with-x \
    --with-gstreamer \
    "${_wine32opts[@]}"

  make
}

package() {
  depends=(${_depends[@]})

  msg2 "Packaging Wine-32..."
  cd "$srcdir/$pkgname-32-build"

  if [[ $CARCH == i686 ]]; then
    make prefix="$pkgdir/usr" install
  else
    make prefix="$pkgdir/usr" \
      libdir="$pkgdir/usr/lib32" \
      dlldir="$pkgdir/usr/lib32/wine" install

    msg2 "Packaging Wine-64..."
    cd "$srcdir/$pkgname-64-build"
    make prefix="$pkgdir/usr" \
      libdir="$pkgdir/usr/lib" \
      dlldir="$pkgdir/usr/lib/wine" install
  fi
}

# vim:set ts=8 sts=2 sw=2 et:
